/*****************************************************************************/
/* An empty STARI stage (see Greenstreet's PhD thesis)                       */
/*****************************************************************************/

module emptystage;

delay logic = <100,200>;

input xtin;
input xfin;
input ackin;

output xt={logic};
output xf={logic};
output ack={true,logic};

gate ct;
 ackin &  xtin -> xt+
~ackin & ~xtin -> xt-
endgate

gate cf;
 ackin &  xfin -> xf+
~ackin & ~xfin -> xf-
endgate

gate ack;
 xt |  xf -> ack-
~xt & ~xf -> ack+
endgate
endmodule

/*****************************************************************************/
/* A full STARI stage (see Greenstreet's PhD thesis)                         */
/*****************************************************************************/

module fullstage;

delay logic = <100,200>;

input xtin;
input xfin;
input ackin;

output xt={true,logic};
output xf={logic};
output ack={logic};

gate ct;
~ackin & ~xtin -> xt-
 ackin &  xtin -> xt+
endgate

gate cf;
 ackin &  xfin -> xf+
~ackin & ~xfin -> xf-
endgate

gate ack;
~xt & ~xf -> ack+
 xt |  xf -> ack-
endgate
endmodule

/*****************************************************************************/
/* A collection of N STARI stages (see Greenstreet's PhD thesis)             */
/* 	In order to add a stage, simply copy a stari instance and increment  */
/*      all the numbers.  Also, increment the numbers in the right and       */
/*      notempty processes.  First N/2 stages must be empty, second N/2 must */
/*      alternate ending with a fullstage.                                   */
/*****************************************************************************/

module stari;

delay pi    = <1200,1200>;
delay skew  = <0,100>;

input clk={pi};
input ack11={true,skew};
input x0t={skew};
input x0f={skew};

emptystage stage1(xtin => x0t, xfin => x0f, ackin => ack2,
                  xt => x1t, xf => x1f, ack => ack1);
emptystage stage2(xtin => x1t, xfin => x1f, ackin => ack3,
                  xt => x2t, xf => x2f, ack => ack2);
emptystage stage3(xtin => x2t, xfin => x2f, ackin => ack4,
                  xt => x3t, xf => x3f, ack => ack3);
emptystage stage4(xtin => x3t, xfin => x3f, ackin => ack5,
                 xt => x4t, xf => x4f, ack => ack4);
emptystage stage5(xtin => x4t, xfin => x4f, ackin => ack6,
                  xt => x5t, xf => x5f, ack => ack5);
fullstage stage6(xtin => x5t, xfin => x5f, ackin => ack7,
                 xt => x6t, xf => x6f, ack => ack6);
emptystage stage7(xtin => x6t, xfin => x6f, ackin => ack8,
                  xt => x7t, xf => x7f, ack => ack7);
fullstage stage8(xtin => x7t, xfin => x7f, ackin => ack9,
                 xt => x8t, xf => x8f, ack => ack8);
emptystage stage9(xtin => x8t, xfin => x8f, ackin => ack10,
                  xt => x9t, xf => x9f, ack => ack9);
fullstage stage10(xtin => x9t, xfin => x9f, ackin => ack11,
                  xt => x10t, xf => x10f, ack => ack10);

process clk;
    *[ clk+; clk- ]
endprocess

process left;
    *[ [clk]; [ true -> x0t+
              | true -> x0f+
	      ]; [~clk];
              [ x0t -> x0t-
              | x0f -> x0f-
              ]
     ]
endprocess

constraint notfull1;
~ack1 -> x0t-
endconstraint

constraint notfull2;
~ack1 -> x0f-
endconstraint

process right;
    *[ [clk]; ack11-; [~clk]; ack11+ ]
endprocess

constraint notempty;
x10t | x10f -> ack11-
endconstraint

endmodule
