/****************************************************************************
 ** 4-bit Comparator Look Ahead Unit
 ** INPUTS: Req, G[3:0], L[3:0], and E[3:0]
 ** OUTPUTS: E, G, L, and Ack
 ****************************************************************************/
module clag;

  input  Req = {false, <300,900>};

  input   G0 = {false, <300,900>};
  input   L0 = {false, <300,900>};
  input   E0 = {false, <300,900>};

  input   G1 = {false, <300,900>};
  input   L1 = {false, <300,900>};
  input   E1 = {false, <300,900>};

  input   G2 = {false, <300,900>};
  input   L2 = {false, <300,900>};
  input   E2 = {false, <300,900>};

  input   G3 = {false, <300,900>};
  input   L3 = {false, <300,900>};
  input   E3 = {false, <300,900>};

  output Ack = {false, <350,1500>};
  output   G = {false, <350,1500>};
  output   L = {false, <350,1500>};
  output   E = {false, <350,1500>};

  gate G;
    (Req & ( G3 | E3 & G2 | E3 & E2 & G1 | E3 & E2 & E1 & G0 ) ) -> G+
    (~Req | (~G3 & ~E3) | 
            (~G3 & ~E2 & ~G2) | 
            (~G3 & ~G2 & ~G1 & ~E1) |
            (~G3 & ~G2 & ~G1 & ~G0) ) -> G-
  endgate

  gate L;
    (Req & ( L3 | E3 & L2 | E3 & E2 & L1 | E3 & E2 & E1 & L0 ) ) -> L+
    (~Req | (~L3 & ~E3) | 
            (~L3 & ~E2 & ~L2) | 
            (~L3 & ~L2 & ~L1 & ~E1) |
            (~L3 & ~L2 & ~L1 & ~L0) ) -> L-
  endgate

  gate E;
    ( Req & E3 & E2 & E1 & E0 ) -> E+
    ( ~Req | ~E3 | ~E2 | ~E1 | ~E0 ) -> E-
  endgate

  gate Ack;
    (G | L | E) -> Ack+
    (~G & ~L & ~E ) -> Ack-
  endgate

  process Comparator3;
    *[ [   true -> ( G3+ ) ; [~Req & Ack] ; G3- ; [~Ack]
         | true -> ( L3+ ) ; [~Req & Ack] ; L3- ; [~Ack]
         | true -> ( E3+ ) ; [~Req & Ack] ; E3- ; [~Ack]
       ]
     ]
  endprocess

  process Comparator2;
    *[ [   true -> ( G2+ ) ; [~Req & Ack] ; G2- ; [~Ack]
         | true -> ( L2+ ) ; [~Req & Ack] ; L2- ; [~Ack]
         | true -> ( E2+ ) ; [~Req & Ack] ; E2- ; [~Ack]
       ]
     ]
  endprocess

  process Comparator1;
    *[ [   true -> ( G1+ ) ; [~Req & Ack] ; G1- ; [~Ack]
         | true -> ( L1+ ) ; [~Req & Ack] ; L1- ; [~Ack]
         | true -> ( E1+ ) ; [~Req & Ack] ; E1- ; [~Ack]
       ]
     ]
  endprocess

  process Comparator0;
    *[ [   true -> ( G0+ ) ; [~Req & Ack] ; G0- ; [~Ack]
         | true -> ( L0+ ) ; [~Req & Ack] ; L0- ; [~Ack]
         | true -> ( E0+ ) ; [~Req & Ack] ; E0- ; [~Ack]
       ]
     ]
  endprocess

  process env;
    *[ [(G3 | L3 | E3 ) & (G2 | L2 | E2 ) & 
        (G1 | L1 | E1 ) & (G0 | L0 | E0 )] ; Req+ ; [Ack] ; Req- ;
       [~G3 & ~L3 & ~E3 & ~G2 & ~L2 & ~E2 & 
        ~G1 & ~L1 & ~E1 & ~G0 & ~L0 & ~E0] ; [~Ack]
     ]
  endprocess

endmodule

