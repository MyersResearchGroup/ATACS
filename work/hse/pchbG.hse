/*****************************************************************************/
/* Martin's precharged half-buffer circuit (see ARVLSI97).                   */
/*****************************************************************************/
module pchb;

delay gatedelay = <90,110>;

input l0={gatedelay};
input l1={gatedelay};
input re={gatedelay};
output le={true,gatedelay};
output r0={gatedelay};
output r1={gatedelay};
output r0set;
output r0reset;
output r1set;
output r1reset;
output rebar={true};

gate reinv;
re -> rebar-
~re -> rebar+
endgate
 
gate legate;
r1 | r0 -> le-
~l1 & ~l0 & ~r1 & ~r0 -> le+
endgate

gate r0set;
re & l0 & le -> r0set+
~re | ~l0 | ~le -> r0set-
endgate

gate r0reset;
rebar & ~le & r0 -> r0reset+
~rebar | le | ~r0 -> r0reset-
endgate

gate r0gate;
r0set & ~r0reset -> r0+
~r0set & r0reset -> r0-
endgate

gate r1set;
re & l1 & le -> r1set+
~re | ~l1 | ~le -> r1set-
endgate

gate r1reset;
rebar & ~le & r1 -> r1reset+
~rebar | le | ~r1 -> r1reset-
endgate

gate r1gate;
r1set & ~r1reset -> r1+
~r1set & r1reset -> r1-
endgate

process r;
*[re+; [r0 | r1]; re-; [~r0 & ~r1]]
endprocess

process l;
*[[ skip -> l0+; [~le]; l0-; [le]
  | skip -> l1+; [~le]; l1-; [le]
 ]]
endprocess
endmodule
